package org.xtuml.bp.xtext.masl.ui.document

import com.google.inject.Inject
import org.eclipse.jface.resource.JFaceResources
import org.eclipse.swt.SWT
import org.eclipse.swt.layout.GridData
import org.eclipse.swt.layout.GridLayout
import org.eclipse.swt.widgets.Composite
import org.eclipse.swt.widgets.Label
import org.eclipse.xtext.ui.IImageHelper
import org.eclipse.xtext.ui.editor.XtextEditor
import org.xtuml.bp.ui.text.AbstractModelElementPropertyEditorInput
import org.eclipse.ui.texteditor.IElementStateListener

class MaslSnippetEditor extends XtextEditor {
	
	@Inject IImageHelper imageHelper
	 
	Label signatureLabel 
	
	override isSaveAsAllowed() {
		false
	}
	
	override createPartControl(Composite parent) {
		val input = editorInput
		if(input instanceof AbstractModelElementPropertyEditorInput) {
			val maslDocumentProvider = documentProvider as MaslDocumentProvider
			val header = maslDocumentProvider.getHeader(input)
			if(header != null) {
				val comp = new Composite(parent, SWT.NONE);
				comp.layout = new GridLayout => [
					numColumns = 1
				]
				signatureLabel = new Label(comp, SWT.WRAP)
				signatureLabel.font = JFaceResources.getFont(JFaceResources.TEXT_FONT)
				super.createPartControl(comp)
				comp.children.get(1).layoutData = new GridData => [
					horizontalAlignment = GridData.FILL
					verticalAlignment = GridData.FILL
					grabExcessHorizontalSpace = true
					grabExcessVerticalSpace = true
				]
			} else {
				super.createPartControl(parent)
			}
			updateLabelAndVisibleRegion(maslDocumentProvider, input)
		} else {
			super.createPartControl(parent)
		}
		documentProvider.addElementStateListener(new IElementStateListener() {
			
			override elementContentAboutToBeReplaced(Object element) {
			}
			
			override elementContentReplaced(Object element) {
				if(editorInput instanceof AbstractModelElementPropertyEditorInput)
					updateLabelAndVisibleRegion(documentProvider as MaslDocumentProvider, editorInput as AbstractModelElementPropertyEditorInput)
			}
			
			override elementDeleted(Object element) {
			}
			
			override elementDirtyStateChanged(Object element, boolean isDirty) {
			}
			
			override elementMoved(Object originalElement, Object movedElement) {
			}
		})
	}	
	
	override getDefaultImage() {
		if(editorInput instanceof AbstractModelElementPropertyEditorInput) 
			imageHelper.getImage(editorInput.imageDescriptor)
		else
			super.getDefaultImage()
	}
	
	def updateLabelAndVisibleRegion(MaslDocumentProvider maslDocumentProvider, AbstractModelElementPropertyEditorInput input) {
		partName = input.name
		val prefix = maslDocumentProvider.getPrefix(input)
		val editable = maslDocumentProvider.getEditable(input)
		sourceViewer.setVisibleRegion(prefix.length, editable.length)
		if(signatureLabel != null) {
			signatureLabel.text = maslDocumentProvider.getHeader(input)
			signatureLabel.parent.layout(true)
		}
	}
}
	